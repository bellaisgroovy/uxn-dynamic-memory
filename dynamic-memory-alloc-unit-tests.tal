( Dynamic memory allocation using pages and a bitmap )
( Unit tests for each function except `malloc` and `free` )

( This code will not assemble unless you've added all functions to be tested )

|0000
( zero page temporary storage )
@idx $2 @jj $2
@n_pages $2
@byte_idx $2
@bit_idx $2
@byte $1

|0100
    test-mask_clear
    test-mask_set
    test-get_bit
    test-clear_bit 
    test-set_bit 
    test-alloc_sz_is_free_at_idx
    test-claim_alloc_sz_at_idx
    test-free_alloc_sz_at_idx
    test-free
    test-malloc
BRK

( All unit tests )

@test-mask_clear ( -- )
    { 001d "Test 20 "for 20 "mask_clear(bit_idx) 0a } STH2r print-str
    #0000 mask_clear ( 1111 1110 ) #fe EQU #30 ADD #18 DEO #0a18 DEO
    #0001 mask_clear ( 1111 1101 ) #fd EQU #30 ADD #18 DEO #0a18 DEO
    #0002 mask_clear ( 1111 1011 ) #fb EQU #30 ADD #18 DEO #0a18 DEO
    #0003 mask_clear ( 1111 0111 ) #f7 EQU #30 ADD #18 DEO #0a18 DEO
    #0004 mask_clear ( 1110 1111 ) #ef EQU #30 ADD #18 DEO #0a18 DEO
    #0005 mask_clear ( 1101 1111 ) #df EQU #30 ADD #18 DEO #0a18 DEO
    #0006 mask_clear ( 1011 1111 ) #bf EQU #30 ADD #18 DEO #0a18 DEO
    #0007 mask_clear ( 0111 1111 ) #7f EQU #30 ADD #18 DEO #0a18 DEO
JMP2r

@test-mask_set ( -- )
    { 001b "Test 20 "for 20 "mask_set(bit_idx) 0a } STH2r print-str
    #0000 mask_set ( 0000 0001 ) #01 EQU #30 ADD #18 DEO #0a18 DEO
    #0001 mask_set ( 0000 0010 ) #02 EQU #30 ADD #18 DEO #0a18 DEO
    #0002 mask_set ( 0000 0100 ) #04 EQU #30 ADD #18 DEO #0a18 DEO
    #0003 mask_set ( 1111 0111 ) #08 EQU #30 ADD #18 DEO #0a18 DEO
    #0004 mask_set ( 1110 1111 ) #10 EQU #30 ADD #18 DEO #0a18 DEO
    #0005 mask_set ( 1101 1111 ) #20 EQU #30 ADD #18 DEO #0a18 DEO
    #0006 mask_set ( 1011 1111 ) #40 EQU #30 ADD #18 DEO #0a18 DEO
    #0007 mask_set ( 0111 1111 ) #80 EQU #30 ADD #18 DEO #0a18 DEO
JMP2r

( 1/ test with 8 bytes in different locations with a different bit set )
( 2/ test with 8 bytes in different locations with multiple bits set )
( In both cases, read bits that are set and not set )
@test-get_bit ( -- )
    { 0016 "Test 20 "for 20 "get_bit(idx) 0a } STH2r print-str
    ( Set some bits in some bytes in the bitmap )
    #01 ;bitmap #0001 ADD2 STA ( positions: 16=1,15=0 )
    #80 ;bitmap #0003 ADD2 STA ( positions: 24=1,25=0 )
    #02 ;bitmap #0004 ADD2 STA ( positions: 39=1,40=0 )
    #40 ;bitmap #0008 ADD2 STA ( positions: 66=1,67=0 )
    #04 ;bitmap #000c ADD2 STA ( positions: 101=1,102=0 )
    #20 ;bitmap #000d ADD2 STA ( position: 114=1,115=9 )
    ( Check if getting the bits from the bitmap works )
    #000f get_bit print-bit ws #000e get_bit print-bit nl
    #0018 get_bit print-bit ws #0019 get_bit print-bit nl
    #0026 get_bit print-bit ws #0027 get_bit print-bit nl
    #0041 get_bit print-bit ws #0042 get_bit print-bit nl
    #0065 get_bit print-bit ws #0066 get_bit print-bit nl
    #006a get_bit print-bit ws #006b get_bit print-bit nl
    ( Set some more bits in some bytes in the bitmap )
    #03 ;bitmap #0001 ADD2 STA ( positions: 16=1,15=0 )
    #c0 ;bitmap #0003 ADD2 STA ( positions: 24=1,25=0 )
    #06 ;bitmap #0004 ADD2 STA ( positions: 39=1,40=0 )
    #60 ;bitmap #0008 ADD2 STA ( positions: 66=1,67=0 )
    #0c ;bitmap #000c ADD2 STA ( positions: 101=1,102=0 )
    #30 ;bitmap #000d ADD2 STA ( position: 114=1,115=9 )
    ( Check again if getting the bits from the bitmap works )
    #000f get_bit print-bit ws #000e get_bit print-bit nl
    #0018 get_bit print-bit ws #0019 get_bit print-bit nl
    #0026 get_bit print-bit ws #0027 get_bit print-bit nl
    #0041 get_bit print-bit ws #0042 get_bit print-bit nl
    #0065 get_bit print-bit ws #0066 get_bit print-bit nl
    #006a get_bit print-bit ws #006b get_bit print-bit nl
JMP2r

@test-clear_bit ( -- )
    { 0018 "Test 20 "for 20 "clear_bit(idx) 0a } STH2r print-str
    ( Clear some bits )
    #000f clear_bit #000e clear_bit
    #0018 clear_bit #0019 clear_bit
    #0026 clear_bit #0027 clear_bit
    #0041 clear_bit #0042 clear_bit
    #0065 clear_bit #0066 clear_bit
    #006a clear_bit #006b clear_bit
    ( Check if the bits were cleared )
    #000f get_bit print-bit ws #000e get_bit print-bit nl
    #0018 get_bit print-bit ws #0019 get_bit print-bit nl
    #0026 get_bit print-bit ws #0027 get_bit print-bit nl
    #0041 get_bit print-bit ws #0042 get_bit print-bit nl
    #0065 get_bit print-bit ws #0066 get_bit print-bit nl
    #006a get_bit print-bit ws #006b get_bit print-bit nl
JMP2r

@test-set_bit ( -- )
    { 0016 "Test 20 "for 20 "set_bit(idx) 0a } STH2r print-str
    ( Set some bits )
    #000f set_bit #000e set_bit
    #0018 set_bit #0019 set_bit
    #0026 set_bit #0027 set_bit
    #0041 set_bit #0042 set_bit
    #0065 set_bit #0066 set_bit
    #006a set_bit #006b set_bit
    ( Check if the bits were set )
    #000f get_bit print-bit ws #000e get_bit print-bit nl
    #0018 get_bit print-bit ws #0019 get_bit print-bit nl
    #0026 get_bit print-bit ws #0027 get_bit print-bit nl
    #0041 get_bit print-bit ws #0042 get_bit print-bit nl
    #0065 get_bit print-bit ws #0066 get_bit print-bit nl
    #006a get_bit print-bit ws #006b get_bit print-bit nl
JMP2r

@test-alloc_sz_is_free_at_idx ( -- )
    { 002f "Test 20 "for 20 "alloc_sz_is_free_at_idx(idx,alloc_sz) 0a } STH2r print-str 
    ( All 0: none free )
    { 0006 "part1 0a } STH2r print-str
    #000f #0001 alloc_sz_is_free_at_idx  print-bit nl
    #000f #0002 alloc_sz_is_free_at_idx  print-bit nl
    #000f #0004 alloc_sz_is_free_at_idx  print-bit nl
    #000f #0008 alloc_sz_is_free_at_idx  print-bit nl
    #000f #0010 alloc_sz_is_free_at_idx  print-bit nl
     { 0006 "part2 0a } STH2r print-str 
    ( Last is 0 because 24 is set )
    #0010 #0001 alloc_sz_is_free_at_idx  print-bit nl
    #0010 #0002 alloc_sz_is_free_at_idx  print-bit nl
    #0010 #0004 alloc_sz_is_free_at_idx  print-bit nl
    #0010 #0008 alloc_sz_is_free_at_idx  print-bit nl
    #0010 #0010 alloc_sz_is_free_at_idx  print-bit nl
    { 0006 "part3 0a } STH2r print-str 
    ( 11100 because 24 is set )
    #0011 #0001 alloc_sz_is_free_at_idx  print-bit nl
    #0011 #0002 alloc_sz_is_free_at_idx  print-bit nl
    #0011 #0004 alloc_sz_is_free_at_idx  print-bit nl
    #0011 #0008 alloc_sz_is_free_at_idx  print-bit nl
    #0011 #0010 alloc_sz_is_free_at_idx  print-bit nl
    { 0006 "part4 0a } STH2r print-str 
    ( All one: all free )
    #006c #0001 alloc_sz_is_free_at_idx  print-bit nl
    #006c #0002 alloc_sz_is_free_at_idx  print-bit nl
    #006c #0004 alloc_sz_is_free_at_idx  print-bit nl
    #006c #0008 alloc_sz_is_free_at_idx  print-bit nl
    #006c #0010 alloc_sz_is_free_at_idx print-bit nl
JMP2r

@test-claim_alloc_sz_at_idx ( -- )
    { 002d "Test 20 "for 20 "claim_alloc_sz_at_idx(idx,alloc_sz) 0a } STH2r print-str
    ( Claim alloc of 1,2,3,4,5 pages starting at 0 )
    ;bitmap LDA2 STH2 ( stash first 2 bytes )
    { 0006 "part1 0a } STH2r print-str
    #0000 ;bitmap STA2 ( clear the bitmap's first 2 bytes ) 
    #0000 #0001 claim_alloc_sz_at_idx ( 1000 000 )
    ;bitmap LDA DUP print8 ws #80 EQU print-bit nl
    #0001 #0002 claim_alloc_sz_at_idx ( 1110 0000, is 1100 0000 )
    ;bitmap LDA DUP print8 ws #e0 EQU print-bit nl 
    #0003 #0003 claim_alloc_sz_at_idx ( 1111 1100, is 1101 1100 )
    ;bitmap LDA DUP print8 ws #fc EQU print-bit nl 
    #0006 #0004 claim_alloc_sz_at_idx ( 1111 1111 1100 0000 , is 1101 1111 1000 0011 )
    ;bitmap LDA DUP print8 ws
    ;bitmap INC LDA DUP print8 ws #ffc0 EQU2 print-bit nl 
    #000a #0005 claim_alloc_sz_at_idx ( 1111 1111 1100 0000 , is 1101 1111 1000 0011 )
    ;bitmap LDA DUP print8 ws
    ;bitmap INC LDA DUP print8 ws #fffe EQU2 print-bit nl 
    ( restore first two bytes to previous state )
    STH2r ;bitmap STA2
    { 0006 "part2 0a } STH2r print-str
    ( Claim alloc of 16 pages starting at 108 )
    #006c #0010 claim_alloc_sz_at_idx
    #007c #0010 alloc_sz_is_free_at_idx print-bit nl
    #006c #0010 alloc_sz_is_free_at_idx print-bit nl
JMP2r

@test-free_alloc_sz_at_idx ( -- ) 
    { 002c "Test 20 "for 20 "free_alloc_sz_at_idx(idx,alloc_sz) 0a } STH2r print-str
    ( Free 16 pages starting at 108 )
    #006c #0010 free_alloc_sz_at_idx
    #007c #0010 alloc_sz_is_free_at_idx print-bit nl
    #006c #0010 alloc_sz_is_free_at_idx print-bit nl
JMP2r

@test-free ( -- )
    { 000e "Test 20 "for 20 "free 0a } STH2r print-str

    ( claim first bit in memory )
    #0000 #0001 ( idx, alloc_sz ) claim_alloc_sz_at_idx ( TODO doesnt work claim the first 8 bits )
    ( print 0 if first bit is are claimed )
    #0000 #0001 alloc_sz_is_free_at_idx print-bit ws

    ( free first 16 bytes in memory to clear the first bit in the bitmap )
    DMEM_START #0001 free ( free the first bit )
    ( print 1 if first 8 bit is free )
    #0000 #0001 alloc_sz_is_free_at_idx print-bit nl


    ( claim bit 3 and 4 in memory ) ( remember count from 0 )
    #0003 #0002 ( idx, alloc_sz ) claim_alloc_sz_at_idx ( TODO doesnt work claim the first 8 bits )
    ( print 0 if bits are claimed )
    #0003 #0002 alloc_sz_is_free_at_idx print-bit ws

    ( free bytes 48 to 80 in memory to clear the first bit in the bitmap )
    DMEM_START #0030 ADD2 #0020 free ( free the first bit )
    ( print 1 if bits are free )
    #0003 #0002 alloc_sz_is_free_at_idx print-bit nl

    ( claim bit 12 to 19 in memory ) ( remember count from 0 )
    #000c #0008 ( idx, alloc_sz ) claim_alloc_sz_at_idx ( TODO doesnt work claim the first 8 bits )
    ( print 0 if bits are is are claimed )
    #000c #0008 alloc_sz_is_free_at_idx print-bit ws

    ( free bytes 48 to 80 in memory to clear the first bit in the bitmap )
    DMEM_START #00c0 ADD2 #0080 free ( free the first bit )
    ( print 1 if  bits are free )
    #000c #0008 alloc_sz_is_free_at_idx print-bit nl
JMP2r

@clear-bitmap
    ;bitmap ( &bitmap )
    N_PAGES ( &bitmap, n_pages )
    #0000 ( &bitmap, n_pages, page_idx )
    &loop
        ( if page_idx == n_pages stop )
        EQU2k ,&done JCN ( &bitmap, n_pages, page_idx )
        
        ( clear page )
        ROT2 ( n_pages, page_idx, &bitmap )
        ADD2k ( n_pages, page_idx, &bitmap, &bitmap[page_idx] )
        #00 ROT ROT ( n_pages, page_idx, &bitmap, 00,  &bitmap[page_idx] )
        STA ( n_pages, page_idx, &bitmap )

        ( increment counter and loop )
        ROT2 ROT2 ( &bitmap, n_pages, page_idx )
        INC2 ( &bitmap, n_pages, page_idx+1 )
        ,&loop JMP
    &done
        POP2 POP2 POP2
        JMP2r

@print-bitmap
    N_PAGES #0001 SUB2 ( n_pages )
    #0000 ( n_pages, page_idx )
    &loop
        ( if page_idx == n_pages stop )
        EQU2k ,&done JCN ( n_pages, page_idx )
        
        ( print page )
        DUP2 get_bit print-bit ( n_pages, page_idx )

        INC2 ( n_pages, page_idx+1 )
        ,&loop JMP
    &done
        POP2 POP2
        JMP2r

@test-malloc
    { 0010 "Test 20 "for 20 "malloc 0a } STH2r print-str
    ( clear bitmap )
    clear-bitmap 

    ( allocate 16 bytes )
    #0010 malloc ( addr )
    ( if bit 1 is claimed print 1 )
    #0000 #0001 alloc_sz_is_free_at_idx #00 EQU print-bit nl


    ( allocate 16 bytes )
    #0010 malloc ( addr )
    ( if bit 2 is claimed print 1 )
    #0001 #0001 alloc_sz_is_free_at_idx #00 EQU print-bit nl


    ( try to claim too big a space )  
    #0fe0 malloc 
    ( print 1 if failed )
    #0000 EQU2 print-bit nl
    JMP2r

( ==================================================== )

( All subroutines to be tested go here )

@malloc ( n_bytes -- <allocation in bytes> )
    ( if n_bytes is 0 return null )
    DUP2 #0000 EQU2 ,&return-null JCN ( n_bytes )

    ( get alloc_sz )
    #0001 SUB2 ( n_bytes-1 )
    PAGE_SZ DIV2 ( n_bytes-1/page_sz )
    #0001 ADD2 ( alloc_sz )

    ( loop in range alloc_sz )
    N_PAGES
    #0000 ( alloc_sz, N_PAGES, idx )
    &loop

        ( if loop ran alloc_sz times return null )
        EQU2k ,&return-null-loop JCN ( alloc_sz, N_PAGES, idx )

        ( if alloc sz is free then claim it )
        ROT2 (  N_PAGES, idx, alloc_sz )
        OVR2 OVR2 (  N_PAGES, idx, alloc_sz, idx, alloc_sz )
        alloc_sz_is_free_at_idx ,&claim JCN (  N_PAGES, idx, alloc_sz )

        ( else loop )
        ROT2 ( idx, alloc_sz, N_PAGES )
        ROT2 ( alloc_sz, N_PAGES, idx )
        INC2 ( alloc_sz, N_PAGES, idx+1 )
        ,&loop JMP

    &return-null-loop
        POP2
        POP2
    &return-null
        POP2 ( clear stack )
        #0000 ( return null )
        JMP2r

    &claim (  N_PAGES, idx, alloc_sz )
        ROT2 POP2 ( idx, alloc_sz )
        OVR2 (  idx, alloc_sz, idx )
        SWP2 (  idx, idx, alloc_sz )
        claim_alloc_sz_at_idx ( idx )
        PAGE_SZ MUL2 DMEM_START ADD2 ( idx*page_sz+dmem_start )
        JMP2r

( points to a byte in memory, and the number of bytes from there to claim )
( one bit in the bitmap represents a 16 byte page in memory )
@free ( ptr n_bytes )
    SWP2 #f000 SUB2 PAGE_SZ DIV2 ( n_bytes idx )
    SWP2 ( idx n_bytes )
    #0001 SUB2 PAGE_SZ DIV2 #0001 ADD2 ( bytes to pages; idx n_pages )
    ;free_alloc_sz_at_idx JMP2

@get_bit ( idx -- bit ) ( wait can i just do ;bitmap ADD2 LDA?? )
    ( get idx % 8 )
    DUP2 DUP2 ( idx, idx, idx )
    #03 SFT2 #30 SFT2 ( idx, idx, idx rounded to the nearest 8 )
    SUB2 ( idx, idx%8 )

    ( take away index of the bit from 7 )
    #0007 SWP2 SUB2 ( idx, bit_idx )
    SWP2 ( bit_idx, idx )

    ( if index is too big exit )
    #03 SFT2 ( bit_idx, byte_idx )
    N_PAGES #0001 SUB2 ( bit_idx, byte_idx, n_pages-1 )
    GTH2k ( bit_idx, byte_idx, n_pages-1, byte_idx>n_pages-1 )
    ,&invalid JCN POP2 ( bit_idx, byte_idx )

    ( get bitmap[byte_idx] )
    ;bitmap ADD2 LDA ( bit_idx, *bitmap[byte_idx] )

    ( get value of bit at idx finally )
    ( bit_idx, byte )
    ROT ( bit_idx_low, byte, bit_idx_high )
    POP ( bit_idx_low, byte )
    SWP ( byte, bit_idx_low )
    SFT ( byte>>bit_idx )
    #01 AND ( bit )
    JMP2r
    
    &invalid
        { 0016 "Outside 20 "of 20 "page 20 "range 0a } STH2r print-str
        BRK


@set_bit ( idx -- )
    ( byte_idx = idx >> 3 )
    DUP2 ( idx, idx )
    #03 SFT2 ( idx, byte_idx )

    ( bit_idx = 7 - {idx - {byte_idx<<3}} )
    DUP2 ( idx, byte_idx, byte_idx )
    #30 SFT ( idx, byte_idx, byte_idx>><<3 )
    ROT2 ( byte_idx, byte_idx>><<3, idx )
    ADD2 ( byte_idx, idx+byte_idx>><<3 )
    #0007 SWP2 ( byte_idx, 0070, idx+byte_idx>><<3 )
    SUB2 ( byte_idx, bit_idx )

    ( byte = bitmap[byte_idx] )
    SWP2 ( bit_idx, byte_idx )
    ;bitmap ( bit_idx, byte_idx, &bitmap )
    ADD2 ( bit_idx, &bitmap[byte_idx] )
    
    ( bitmap[byte_idx] = byte | mask_set{bit_idx} )
    DUP2 ROT2 SWP2 (  &bitmap[byte_idx], bit_idx, &bitmap[byte_idx] )
    LDA (  &bitmap[byte_idx], bit_idx, *bitmap[byte_idx] )
    ROT ROT ( &bitmap[byte_idx], *bitmap[byte_idx], bit_idx )
    mask_set ( &bitmap[byte_idx], *bitmap[byte_idx], mask )
    ORA ( &bitmap[byte_idx], byteMasked )
    ROT ROT ( byteMasked, &bitmap[byte_idx] )
    STA ( -- )
    JMP2r

@mask_set ( idx16 ) ( returns mask with bit idx spaces left as 1 and the rest 0 )
    SWP POP ( makes idx byte instead of short ) ( idx8 ) ( is this def right? )
    #40 SFT ( moves low nibble of idx to high nibble ) ( idx8<<4 )
    #01 SWP SFT ( moves 1 in 0000 0001 left by value of idx ) ( 1<<idx8<<4 )
    JMP2r

@clear_bit ( idx -- )
    ( get idx % 8 )
    DUP2 DUP2 ( idx, idx, idx )
    #03 SFT2 #30 SFT2 ( idx, idx, idx rounded to the nearest 8 )
    SUB2 ( idx, idx%8 )

    ( take away index of the bit from 7 )
    #0007 SWP2 SUB2 ( idx, bit_idx )

    ( get mask )
    mask_clear ( idx, mask )
    ROT ROT ( mask, idx )

    ( get byte )
    #03 SFT2 ( mask, byte_idx )
    ;bitmap ADD2 ( mask, &bitmap[byte_idx] )
    LDAk ( mask, &bitmap[byte_idx], byte )
    ROT ROT ( mask, byte, &bitmap[byte_idx] )
    SWP2 ( &bitmap[byte_idx], mask, byte )

    ( apply mask )
    AND (&bitmap[byte_idx], mask&byte )

    ( store result )
    ROT ROT ( mask&byte, &bitmap[byte_idx] )
    STA ( -- )

    JMP2r

@mask_clear ( bit_idx -- 0xFF ^ {1 << bit_idx} )
    ( shift 1 to the left by bit_idx )
    SWP POP ( make bit_idx a byte )
    #01 SWP ( 01, bit_idx )
    #40 SFT ( move content of bit_idx to left nibble ) ( 01, bit_idx00 )
    SFT ( 1 << bit_idx )

    ( flip all the bits )
    #ff
    EOR
    
    JMP2r

( allocation size is in pages )
@alloc_sz_is_free_at_idx ( idx alloc_sz -- 00 or 01 )

    ( if out of range return 0 )
    ADD2k N_PAGES #0001 SUB2 ( idx, alloc_sz, idx+alloc_sz, N_PAGES-1 )
    GTH2 ( idx, alloc_sz, result ) 
    ,&out-of-range JCN ( idx, alloc_sz )

    ( if any bits in the alloc range are taken return 0 )
    #0000 ( idx, alloc_sz, counter )
    &loop
        EQU2k ,&free JCN ( if alloc_sz < counter finish loop  )

        ( if bit at idx+counter is allocated return 0 )
        ROT2 ( alloc_sz, counter, idx )
        ADD2k (alloc_sz, counter, idx, counter+idx )
        get_bit #01 EQU (alloc_sz, counter, idx, bit==1 )
        ,&not-free JCN ( alloc_sz, counter, idx )

        ( prepare for next iteration )
        ROT2 ( counter, idx, alloc_sz )
        ROT2 ( idx, alloc_sz, counter )
        INC2
        ,&loop JMP

    &out-of-range
        POP2 POP2 ( clear stack )
        #00 JMP2r ( return 0 )

    &not-free
        POP2 POP2 POP2
        #00 JMP2r

    &free
        POP2 POP2 POP2 ( clear stack )
        #01 JMP2r
    
@claim_alloc_sz_at_idx ( idx alloc_sz )
    #0000 ( idx, alloc_sz, counter )
    &loop
        ( if alloc_sz is less than counter end )
        EQU2k ,&end JCN ( idx, alloc_sz, counter )
        ROT2 ( alloc_sz, counter, idx )

        ( claim bit at bitmap[idx+counter] )
        ADD2k ( alloc_sz, counter, idx, counter+idx )
        set_bit ( alloc_sz, counter, idx )
        ROT2 ( counter, idx, alloc_sz )
        ROT2 ( idx, alloc_sz, counter )

        ( increment counter and loop )
        INC2
        ,&loop JMP
    &end
        POP2 POP2 POP2 ( clears stack )
        JMP2r

@free_alloc_sz_at_idx ( idx alloc_sz ) 
#0000 ( idx, alloc_sz, counter )
    &loop
        ( if alloc_sz is less than counter end )
        EQU2k ,&end JCN ( idx, alloc_sz, counter )
        ROT2 ( alloc_sz, counter, idx )

        ( free bit at bitmap[idx+counter] )
        ADD2k ( alloc_sz, counter, idx, counter+idx )
        clear_bit ( alloc_sz, counter, idx )
        ROT2 ( counter, idx, alloc_sz )
        ROT2 ( idx, alloc_sz, counter )

        ( increment counter and loop )
        INC2
        ,&loop JMP
    &end
        POP2 POP2 POP2 ( clears stack )
        JMP2r

( ==================================================== )
( Functions for printing )
@todo { 0005 "TODO 0a } STH2r print-str JMP2r

@print-bit #30 ADD #18 DEO JMP2r
@nl #0a18 DEO JMP2r
@ws #2018 DEO JMP2r

@print8 ( b -: )
    ( DUP ) ( b b )
    DUP ( b b b )
    lonib ( b b ln )
    h2a ( b b lna )
    SWP ( b lna b )
    hinib ( b lna hn )
    h2a ( b lna hna )
    #18 DEO #18 DEO ( POP )
    JMP2r

@print16 ( a* -: )
    SWP
    print8
    print8
    JMP2r

@lonib ( a -: b )
    #0f AND
    JMP2r

@hinib ( a -: b )
    #04 SFT
    JMP2r

@h2a ( a -: b )
    DUP ( a a )
    #0a LTH ( a c1 )
    DUP ( a c1 c1 )
    #01 SWP SUB ( a c1 1-c1 )
    #57 MUL ( a c1 [1-c1]*57 )
    SWP ( a [1-c1]*57 c1 )
    #30 MUL ( a [1-c1]*57 c1*30 )
    ADD 
    ADD ( a+[1-c1]*57+c1*30 )
    JMP2r

( To print a 1-byte unsigned integer 
as a decimal number of 3 digits )
( a -: )
@print8-dec
    ( h = x/100 )
    DUP ( x x ) 
    DUP #64  LTH ,&d2 JCN 
    #64 DIV ( x h ) DUP #30 ADD #18 DEO ( x h )
    ( d = x - 100*h )
    #64 MUL SUB ( d )
    ( t = d / 10 )
    DUP ( d d )
    &d2
    DUP #0a  LTH ,&d3 JCN
    #0a DIV ( d t ) DUP #30 ADD #18 DEO ( d t )
    ( e = d - 10*t )
    #0a MUL SUB ( e ) 
    DUP ( e e )
    &d3
    #30 ADD #18 DEO
    POP
JMP2r
( To print a 2-byte unsigned integer 
as a decimal number of 5 digits )
( x:16 -> -- )
( x* -: )
@print16-dec
    DUP2 ( x:16 x:16 )
    DUP2 #2710 LTH2 ,&d2 JCN
    ( T = x/10000 )
    #2710 DIV2 ( x:16 T:16 ) DUP #30 ADD #18 DEO
    ( Dt = x - 10000*T )
    #2710 MUL2 SUB2 ( Dt )
    DUP2 ( Dt Dt )
    &d2
    DUP2 #03e8  LTH2 ,&d3 JCN
    ( D = Dt / 1000 )
    #03e8 DIV2 ( Dt D ) DUP #30 ADD #18 DEO
    ( ht = Dt-D*1000 )
    #03e8 MUL2 SUB2  ( ht )
    DUP2 ( ht ht )
    &d3
    DUP2 #0064  LTH2 ,&d4 JCN
    ( h = ht /100 )
    #0064 DIV2 ( ht h ) DUP #30 ADD #18 DEO
    ( tt = ht - h*100 )
    #0064 MUL2 SUB2 ( tt )
    DUP2 ( tt tt )
    &d4
    ( t = tt / 10 )
    #000a DIV2 ( tt t ) DUP #30 ADD #18 DEO
    ( e = tt - 10*t )
    #000a MUL2 SUB2 ( e ) #30 ADD #18 DEO POP
    JMP2r

( To print a string )
( The string starts with a 2-byte lenght field )
@print-str ( str* -: )
    LDA2k ( ;str size )
    SWP2 #0002 ADD2 SWP2 ( ;str+2 size )
    DUP2 ( ;str+2 size size )
    ( So we test if that is 0 and if not we keep going )
    &loop
    #0000 EQU2 ,&exit JCN ( ;str+2 size )
    SWP2 ( size ;str+2 )
    LDAk #18 DEO ( size ;str+2 )
    INC2 SWP2 #0001 SUB2 DUP2 ( ;str+3 size-1 size-1 )
    ,&loop JMP
    &exit
    POP2 POP2
    JMP2r

( example strings )
@invalid-str 000f "Invalid 20 "access:
@outside-str 0016 "Outside 20 "of 20 "page 20 "range:

( END of functions for printing and strings )
( ==================================================== )

@PAGE_SZ ( -: a* ) #0010 JMP2r ( We allocate 16 bytes per page )
@N_PAGES ( -: a* ) #0100 JMP2r ( We have a total of 256 pages, so we can allocate at most 4kB )
@DMEM_START ( -: a* ) #f000 JMP2r ( Start at 60*1024 )

 ( N_PAGES bits, packed in bytes mean N_PAGES/8 entries, so with the above, the bitmap will take 64 bytes )
 ( 0 means free )
@bitmap $40

( The allocation happens below this )
|f000
@dmem
